plugins {
    id 'org.springframework.boot' version '2.6.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

ext {
    set('springBootVersion', "2.6.5")
    set('springDependencyVersion', "1.0.11.RELEASE")
    set('lombokVersion', '1.18.22')
    set('slf4jVersion', '1.7.30')
    set('springdocVersion', '1.6.7')
    set('mysqlDriverVersion', '8.0.25')
    set("mybatisPlusVersion", "3.4.2")
    set("hutoolVersion", "4.5.7")
}


allprojects { project ->
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "java"
    apply plugin: "idea"

    group = 'com.soulcraft'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    idea {
        module {
            downloadSources = true
        }
    }

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        options.encoding = 'UTF-8'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        all {
            resolutionStrategy.cacheChangingModulesFor 0, 'hours'
        }
    }

    jar {
        manifest {
            attributes(
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.version,
                    "Build-JDK": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    "Build-OS": "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }

    repositories {
        mavenLocal()
    }

    dependencies {
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        implementation "org.springdoc:springdoc-openapi-ui:${springdocVersion}"

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}